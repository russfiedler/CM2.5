#!/bin/csh -f
unalias *
set echo
#------------------------------------------------------------------------------------------------------------------------------
set Root = $cwd/..                                    # The root directory; where the release package was extracted.
set platform  = intel                                 # A unique identifier for your platform.
set template      = $Root/bin/$platform.mk            # path to template for your platform.
set mkmf          = $Root/bin/mkmf                    # path to executable mkmf.
set sourcedir     = $Root/src                         # path to directory containing model source code.
set mppnccombine  = $Root/bin/mppnccombine.$platform  # path to executable mppnccombine.
set landnccombine = $Root/bin/landnccombine.$platform # path to executable landnccombine.
set PPDIR         = $Root/postprocessing              # path to directory containing postprocessing source code for combining netcdf files.
set FGDIR         = $Root/fregrid                     # path to root directory containing postprocessing source code and data for interpolation to lat-lon grid.
set execdir = $cwd/exec.$platform                     # where code is compiled and executable is created.
set executable = $execdir/CM2.5.x                     # path to model executable.
set pathnames = $cwd/path_names                       # path to file containing list of source paths.
#------------------------------------------------------------------------------------------------------------------------------
# Setting one or more of these to something other than "yes" will cause the script to skip the section indicated.
set compile_mppnccombine = yes
set compile_landnccombine = yes
set compile_fregrid = yes
set compile_model = yes
#------------------------------------------------------------------------------------------------------------------------------

source /opt/modules/3.2.6.6/init/csh
module use -a /ncrc/home2/fms/local/modulefiles
module unload PrgEnv-pgi PrgEnv-pathscale PrgEnv-intel PrgEnv-gnu PrgEnv-cray
module unload fre
module unload fre-system
module load PrgEnv-intel/4.0.46
module swap intel intel/12.1.3.293
module load netcdf
module list

#------------------------------------------------------------------------------------------------------------------------------
if ( $compile_mppnccombine == yes ) then
   # --- compile mppnccombine.c ---
   cd $PPDIR
   cc -O -c -I/opt/cray/netcdf/default/intel/120/include mppnccombine.c
   if ( $status != 0 ) exit
   cc -O -o $mppnccombine /opt/cray/netcdf/default/intel/120/lib/libnetcdf_intel.a mppnccombine.o
   if ( $status != 0 ) exit
endif
#------------------------------------------------------------------------------------------------------------------------------
if ( $compile_landnccombine == yes ) then
   ftn -c -i4 -r8  -I/opt/cray/netcdf/default/intel/120/include nfu.F90
   if ( $status != 0 ) exit
   ftn -c -i4 -r8  -I/opt/cray/netcdf/default/intel/120/include nfu_compress.F90
   if ( $status != 0 ) exit
   ftn -c -i4 -r8  -I/opt/cray/netcdf/default/intel/120/include combine-ncc.F90
   if ( $status != 0 ) exit
   ftn -o $landnccombine -lnetcdff nfu.o nfu_compress.o combine-ncc.o
   if ( $status != 0 ) exit
endif
#------------------------------------------------------------------------------------------------------------------------------
if ( $compile_fregrid == yes ) then
   cd $FGDIR/src/tools/fregrid
   setenv KMP_STACKSIZE 512m
   setenv NC_BLKSZ 1M
   setenv SITE gaea
   make HDF5_HOME=/opt/cray/hdf5/1.8.8/intel/120 NETCDF_HOME=/opt/cray/netcdf/default/intel/120 -f fre-nctools.mk
   if ( $status != 0 ) exit
   cp fregrid_parallel $Root/bin/fregrid_parallel.$platform
endif
#------------------------------------------------------------------------------------------------------------------------------
if ( $compile_model == yes ) then
   # setup directory structure
   if ( ! -d $execdir ) mkdir -p $execdir
   cd $execdir

   # execute mkmf to create makefile
   set cppDefs = "-Duse_libMPI -Duse_netCDF -DSPMD -DUSE_OCEAN_BGC -DENABLE_ODA -DINTERNAL_FILE_NML -DLAND_BND_TRACERS -DMAXDIMVALS_=5000000 -DMAXFIELDMETHODS_=200 -DMAXFIELDS_=200 -Duse_shared_pointers -DOLD_COS_SG -DOLD_PT_TO_T -DUSE_LOG_DIAG_FIELD_INFO -Duse_LARGEFILE"
   $mkmf -a $sourcedir -t $template -p $executable:t -c "$cppDefs" -I$sourcedir/mom5/ocean_param/gotm-4.0/include -I$sourcedir/shared/include -I$sourcedir/shared/mpp/include /usr/local/include $pathnames
   if ( $status != 0 ) then
      unset echo
      echo "ERROR: mkmf failed for CM2.5" 
      exit 1
   endif

   # --- execute make ---
   make $executable:t
   if ( $status != 0 ) then
      unset echo
      echo "ERROR: make failed for CM2.5" 
     exit 1
   endif

   unset echo
   echo "NOTE: make successful for CM2.5"
endif
